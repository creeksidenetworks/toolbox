#!ipxe

:global_vars
set base_url        http://download.creekside.network/ipxe

:version_menu
menu Choose Rocky Linux Version
item --gap -- --------------------------------------------------------------------------
item rocky8   Rocky Linux 8
item rocky9   Rocky Linux 9
item rocky10   Rocky Linux 10
item --gap -- --------------------------------------------------------------------------
item back     Back to Main Menu
item --gap -- --------------------------------------------------------------------------
choose version && goto ${version}

:back
chain ${base_url}/boot.ipxe

:rocky8
set version 8
goto desktop_light

:rocky9
set version 9
goto desktop_light

:rocky10
set version 10
goto desktop_full

:desktop_light
menu Choose Desktop Environment
item --gap -- --------------------------------------------------------------------------
item mini     None
item xfce     Xfce
item mate     Mate
item --gap -- --------------------------------------------------------------------------
item version_menu     Back to Version Selection
item --gap -- --------------------------------------------------------------------------
choose desktop && goto ${desktop}

:desktop_full
menu Choose Desktop Environment
item --gap -- --------------------------------------------------------------------------
item mini     None
item kde      KDE Workspace
item --gap -- --------------------------------------------------------------------------
item version_menu     Back to Version Selection
item --gap -- --------------------------------------------------------------------------
choose desktop && goto ${desktop}


:mini
set desktop_type mini
goto partition_menu

:xfce
set desktop_type xfce
goto partition_menu

:mate
set desktop_type mate
goto partition_menu

:kde
set desktop_type kde
goto partition_menu

:partition_menu
menu Choose Partition Mode
item --gap -- --------------------------------------------------------------------------
item auto     Auto Partitioning
item manual   Manual Partitioning
item --gap -- --------------------------------------------------------------------------
item desktop_menu     Back to Desktop Selection
item --gap -- --------------------------------------------------------------------------
choose partition && goto ${partition}

:auto
set partition_mode auto
goto region_menu

:manual 
set partition_mode manual
goto region_menu

:region_menu
menu Choose Region
item --gap -- --------------------------------------------------------------------------
item global   US - Official Rocky Server
item china    CN - Nanjing University Mirror
item uk       UK - Kent University Mirror
item --gap -- --------------------------------------------------------------------------
item partition_menu     Back to Partition Selection
item --gap -- --------------------------------------------------------------------------
choose region && goto ${region}

:global
set base-rocky-url  http://dl.rockylinux.org/pub/rocky
set region global
goto launch

:china
set base-rocky-url  http://mirror.nju.edu.cn/rocky
set region china
goto launch

:uk
set base-rocky-url  http://rockylinux.mirrorservice.org/pub/rocky
set region uk
goto launch

:launch
kernel ${base-rocky-url}/${version}/BaseOS/x86_64/os/images/pxeboot/vmlinuz initrd=initrd.img inst.repo=${base-rocky-url}/${version}/BaseOS/x86_64/os inst.ks=${base_url}/rocky/rocky.ks desktop=${desktop_type} region=${region} partition=${partition_mode}
initrd ${base-rocky-url}/${version}/BaseOS/x86_64/os/images/pxeboot/initrd.img
boot
